-- HANOIacts

preds:	        Initial Final EF
constructs:	A B C AB BA AC CA BC CB
defuncts:	procs lg start draw piles tow
fovars: 	st xs ys zs

axioms:

lg == length(procs) &

start == A(procs)^B[]^C[] & states == [start] & 
labels == [AB,BA,AC,CA,BC,CB] &

(A(x:xs)^B[],AB) -> A(xs)^B[x]	&
(A(x:xs)^C[],AC) -> A(xs)^C[x]	&
(A[]^B(x:xs),BA) -> A[x]^B(xs) 	&
(A[]^C(x:xs),CA) -> A[x]^C(xs) 	&
(B[]^C(x:xs),CB) -> B[x]^C(xs) 	&
(B(x:xs)^C[],BC) -> B(xs)^C[x] 	&

(x < y ==> (A(x:xs)^B(y:ys),AB) -> A(xs)^B(x:y:ys))	&
(y < x ==> (A(x:xs)^B(y:ys),BA) -> A(y:x:xs)^B(ys))	&
(x < y ==> (A(x:xs)^C(y:ys),AC) -> A(xs)^C(x:y:ys))	&
(y < x ==> (A(x:xs)^C(y:ys),CA) -> A(y:x:xs)^C(ys))	&
(x < y ==> (B(x:xs)^C(y:ys),BC) -> B(xs)^C(x:y:ys))	&
(y < x ==> (B(x:xs)^C(y:ys),CB) -> B(y:x:xs)^C(ys))	&

(Initial$st <==> st = start) &
(Final$A(xs)^B(ys)^C(zs) <==> ys = procs | zs = procs) &

draw == wtree $ fun(x,ite(x `in` labels)
			 (frameS$text$x,ite(Initial$x|Final$x)
					   (piles$x,x))) &

piles$A(xs)^B(ys)^C(zs) == center$turt[tow$xs,J$16,tow$ys,J$16,tow$zs] &

tow$xs == scale(8)$pileR$lg:xs

conjects: start `sats` EF$Final	&		--> True
	  any(=AB)$labels &
	  map(=AB)$labels 

terms:	fun(x,ite(x `in` labels)
	         (frameS(text(x)),ite(Initial(x) | Final(x))
				     (piles(x),x))) $ (A[]^B[]^C(procs))

-- A : 1 2 3  B :  C :
-- A : 2 3  B :  C : 1
-- A : 3  B : 2  C : 1
-- A : 3  B : 1 2  C :
-- A :  B : 1 2  C : 3
-- A : 1  B : 2  C : 3
-- A : 1  B :  C : 2 3
-- A :  B :  C : 1 2 3

